apply plugin: 'org.jetbrains.dokka'
apply plugin: 'org.jetbrains.kotlin.multiplatform'
apply plugin: 'com.vanniktech.maven.publish'

kotlin {
    /*
    js {
        nodejs()
    }
*/
    jvm()

    iosArm32()
    iosArm64()
    iosX64()
    linuxX64()
    macosX64()
    mingwX64()
    tvosArm64()
    tvosX64()
    watchosArm32()
    watchosArm64()
    watchosX86()

    sourceSets {
        commonMain {
            dependencies {
                api libraries.kotlinStdlibCommon
                api libraries.coroutines
            }
        }

        commonTest {
            dependencies {
                implementation testLibraries.kotlinTest
                implementation testLibraries.kotlinTestAnnotations
                implementation testLibraries.kotestAssertions
                implementation testLibraries.turbine
            }
        }

        jvmMain {
            dependencies {
                implementation libraries.kotlinStdlibJdk8
            }
        }

        jvmTest {
            dependencies {
                implementation testLibraries.kotlinTestJunit
                implementation testLibraries.coroutinesTest
            }
        }

        nativeMain {
            dependencies {
                implementation (libraries.coroutinesNativeMT) {
                    force = true
                }
            }
        }
        nativeTest {
            dependencies {
                implementation (libraries.coroutinesNativeMT) {
                    force = true
                }
            }
        }

        configure([
                targets.iosArm32,
                targets.iosArm64,
                targets.iosX64,
                targets.linuxX64,
                targets.macosX64,
                targets.mingwX64,
                targets.tvosArm64,
                targets.tvosX64,
                targets.watchosArm32,
                targets.watchosArm64,
                targets.watchosX86,
        ]) {
            compilations.main.source(sourceSets.nativeMain)
            compilations.test.source(sourceSets.nativeTest)
        }
    }

    targets.all {
        compilations.all {
            kotlinOptions {
                allWarningsAsErrors = true
                freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
            }
        }
    }

    targets.jvm {
        compilations.all {
            kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }
}
