plugins {
    alias(libs.plugins.fgp.multiplatform)
    alias(libs.plugins.dokka)
    alias(libs.plugins.fgp.publish)
}

freeletics {
    explicitApi()
}

// TODO replace with addCommonTargets() after adding JS support (blocked by Kotlin 1.8.20)
kotlin {
    jvm()

    linuxX64()

    iosArm32()
    iosArm64()
    iosX64()
    iosSimulatorArm64()

    macosArm64()
    macosX64()

    mingwX64()

    tvosArm64()
    tvosX64()

    watchosArm32()
    watchosArm64()
    watchosX86()
    watchosX64()
    watchosSimulatorArm64()

    def nativeMain = sourceSets.create("nativeMain") { sourceSet ->
        sourceSet.dependsOn(sourceSets.getByName("commonMain"))
    }
    def nativeTest = sourceSets.create("nativeTest") { sourceSet ->
        sourceSet.dependsOn(sourceSets.getByName("commonTest"))
    }
    targets.configureEach {
        if (it.platformType.name == "native") {
            it.compilations.getByName("main").source(nativeMain)
            it.compilations.getByName("test").source(nativeTest)
        }
    }
}

dependencies {
    commonMainApi(libs.coroutines.core)
    commonMainApi(libs.mad.statemachine)

    commonTestImplementation(libs.kotlin.test)
    commonTestImplementation(libs.kotlin.test.annotations)
    commonTestImplementation(libs.turbine)
    commonTestImplementation(libs.coroutines.test)
}
