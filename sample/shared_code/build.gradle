plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.cocoapods)
    alias(libs.plugins.codesync)
}

version = "1.0.0"

kotlin {
    jvm()
    iosX64()
    iosArm64()
    iosSimulatorArm64()

    cocoapods {
        summary = "Some description for a Kotlin/Native module"
        homepage = "Link to a Kotlin/Native module homepage"
        name = "shared_code"
        ios.deploymentTarget = "12.2"
        podfile = project.file("../ios/Podfile")
        frameworkName = "shared_code"
    }

    sourceSets {
        all {
            languageSettings {
                optIn("kotlinx.coroutines.FlowPreview")
                optIn("kotlinx.coroutines.ExperimentalCoroutinesApi")
            }
        }

        commonMain {
            dependencies {
                api project(':flowredux')
            }
        }

        iOSMain {
            dependsOn(commonMain)
            iosX64Main.dependsOn(iOSMain)
            iosArm64Main.dependsOn(iOSMain)
            iosSimulatorArm64Main.dependsOn(iOSMain)
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = libs.versions.java.get()
    }
}

xcodeSync {
    projectPath = "../ios/ios.xcodeproj"
    target = "iosApp"
}
